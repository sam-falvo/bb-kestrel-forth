    .p816
    .a16
    .i16

    .include "hw/cpu.inc"
    .include "hw/chipset.inc"


    .bss

    ; Count-down counter for cursor; after `blinker' frames, the state
    ; of the cursor toggles.
blinker:
    .res 2

    .code

.export __n_reset
.import tioInitialize
.import tioTypeCharacter
.import tioCarriageReturn
.import tioShowCursor
.import tioClear
.import tioHome
.import tioTypeString
.proc __n_reset
    ;; This is the native-mode cold-boot entry point.  This procedure
    ;; never returns.

    ; Preconditions:
    ;   S = $7FFF.
    ;   Interrupts disabled.
    ;   A, X, Y in 16-bit mode.
    ;   CPU in native mode.
    ;   RAM-resident exception/interrupt vector table initialized.

    jsr initializeMGIA
    jsr initializeIRQ
    cli
    jsr tioInitialize

    lda #1
    sta blinker
    jsr tioShowCursor

    jsr tioClear
    jsr tioHome
    ldx #Msg1
    ldy #Msg1Len
    jsr tioTypeString
    jsr tioCarriageReturn
    bra *

Msg1:
    .byte "**** Kestrel Keyboard Demonstrator ****"
Msg1Len = *-Msg1

.endproc


.proc initializeMGIA
    ;; Initializes the Monochrome Graphics Interface Adapter.
    ;; The MGIA was originally specified for implementation in an
    ;; FPGA; hence, programming the emulator's version of it resembles
    ;; a more traditional video chip.  In reality, it remains to be seen
    ;; if an FPGA will provide the functionality, or if a SEAforth chip
    ;; will.  If the latter, this code will change markedly.

    lda #800                ; Set CRTC for VGA monitor, 60Hz refresh
    sta CCHZTL
    lda #525
    sta CCVRTL
    lda #$025F
    sta CCSYWD

    lda #140                ; Set display to center of the display
    sta VIHZST              ; (hopefully)
    lda #780
    sta VIHZEN
    lda #34
    sta VIVRST
    lda #514
    sta VIVREN

    inc                     ; Set raster line for display interrupt
    sta CCVRLI
    rts
.endproc


.import nativeModeIRQVector
.proc initializeIRQ
    lda #globalIrqHandler
    sta nativeModeIRQVector

    lda #0
    sta IQPN
    lda #IQVRLI
    sta IQEA
    rts

globalIrqHandler:
    php
    AX16
    pha
    phx
    phy

    jsr processPendingMgiaInterrupt

    ply
    plx
    pla
    plp
    rti
.endproc


.import tioToggleCursor
.proc processPendingMgiaInterrupt
    lda IQPN
    bit #IQVRLI
    beq noPendingInterrupt

    ; acknowledge MGIA interrupt
    and #~IQVRLI
    sta IQPN

    ; reset fetch pointer to beginning of frame buffer
    lda #$0000
    sta VIDAPTL
    lda #$00FE
    sta VIDAPTH

    ; implement cursor blinking logic
    dec blinker
    bne done
    lda #30
    sta blinker
    jsr tioToggleCursor

done:
noPendingInterrupt:
    rts
.endproc

